import org.apache.tools.ant.filters.LineContains

plugins {
    id 'additionalentityattributes-loader'
    id 'net.neoforged.moddev'
}

neoForge {
    version = neoforge_version
    // Automatically enable neoforge AccessTransformers if the file exists
    def at = project(':common').file("src/main/resources/${mod_id}.cfg")
    if (at.exists()) {
        setAccessTransformers(at)
    }
    parchment {
        minecraftVersion = parchment_minecraft
        mappingsVersion = parchment_version
    }
    runs {
        configureEach {
            systemProperty('neoforge.enabledGameTestNamespaces', mod_id)
            ideName = "NeoForge ${it.name.capitalize()} (${project.path})" // Unify the run config names with fabric
        }
        client {
            client()
        }
        server {
            server()
        }
    }
    mods {
        "${mod_id}" {
            sourceSet sourceSets.main
        }
    }
}

processResources {
    filesMatching("neoforge.mods.toml") {
        filter(LineContains, negate: true, contains: ["refmap"])
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }